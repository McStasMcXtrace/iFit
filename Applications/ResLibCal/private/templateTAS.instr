/*******************************************************************************
*
* McStas, neutron ray-tracing package
*         Copyright (C) 1997-2008, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Instrument: Template TAS instrument
*
* %Identification
* Written by: <a href="mailto:farhi@ill.fr">Emmanuel Farhi</a>
* Date: 2006
* Origin: <a href="http://www.ill.fr">ILL (France)</a>
* Release: McStas CVS_080624
* Version: $Revision: 1.9 $
* %INSTRUMENT_SITE: Templates
*
* Template RESCAL type triple-axis machine (TAS)
*
* %Description
* This instrument is a simple model of triple-axis spectrometer.
* It is directly illuminated by the moderator,
* and has curved monochromator and analyzer. 
*
* Si       111 DM=3.135 AA
* PG       002 DM=3.355 AA (Highly Oriented Pyrolythic Graphite)
* Ge       311 DM=1.714 AA
*
* %Example: QM=1 Detector: D7_SC3_1D_I=3.3e-05
* %Example: QM=1 Detector: He3H_I=2.8e-11
*
* %Parameters
* INPUT PARAMETERS:
* KI:   Incoming neutron wavevector [Angs-1]
* EN:   Energy transfer in crystal [meV]
* L1:   Source-Monochromator distance. Contains 1st Collimator of length 5.34 [m]
* L2:   Monochromator-Sample distance. Contains 2nd Collimator of length 0.35 [m]
* L3:   Sample-Analyzer distance. Contains 3rd Collimator of length 0.40 [m]
* L4:   Analyzer-detector distance. Contains 4th Collimator of length 0.24 [m]
* SM:   Scattering sense of beam from Monochromator [1:left, -1:right]
* SS:   Scattering sense of beam from Sample [1:left, -1:right]
* SA:   Scattering sense of beam from Analyzer [1:left, -1:right]
* DM:   Monochromator d-spacing [Angs]
* DA:   Analyzer d-spacing [Angs]
* RMV:  Monochromator vertical curvature, 0 for flat, -1 for automatic setting [m]
* RMH:  Monochromator horizontal curvature, 0 for flat, -1 for automatic setting [m]
* RAV:  Analyzer vertical curvature, 0 for flat, -1 for automatic setting [m]
* RAH:  Analyzer horizontal curvature, 0 for flat, -1 for automatic setting [m]
* ETAM: Monochromator mosaic [arc min]
* ETAA: Analyzer mosaic [arc min]
* ALF1: Horizontal collimation from Source to Monochromator [arc min]
* ALF2: Horizontal collimation from Monochromator to Sample A[arc min]
* ALF3: Horizontal collimation from Sample to Analyzer [arc min]
* ALF4: Horizontal collimation from Analyzer to Detector [arc min]
* BET1: Vertical collimation from Source to Monochromator [arc min]
* BET2: Vertical collimation from Monochromator to Sample A[arc min]
* BET3: Vertical collimation from Sample to Analyzer [arc min]
* BET4: Vertical collimation from Analyzer to Detector [arc min]
* A1:   Monohromator rotation angle [deg]
* A2:   Monohromator take-off angle [deg]
* A3:   Sample rotation angle [deg]
* A4:   Sample take-off angle [deg]
* A5:   Analyzer rotation angle [deg]
* A6:   Analyzer take-off angle [deg]
* verbose: print TAS configuration. 0 to be quiet [1]
*
* WM:   Width  of monochromator [m]
* HM:   Height of monochromator [m]
* NVM:  Number of vertical slabs composing the monochromator [1]
* NHM:  Number of horizontal slabs composing the monochromator [1]
* WA:   Width  of analyzer [m]
* HA:   Height of analyzer [m]
* NVA:  Number of vertical slabs composing the analyzer [1]
* NHA:  Number of horizontal slabs composing the analyzer [1]
* radius:    outer radius of sample hollow cylinder/sphere [m]
* height:    sample height. Use 0 for a spherical shape [m]
* WB:   Width  of the incoming beam/source, at L1 from monochromator [m]
* HB:   Height of the incoming beam/source, at L1 from monochromator [m]
* WD:   Width  of the detector [m]
* HD:   Height of the detector [m]
*
* %Link
* Rescal for Matlab at http://www.ill.eu/instruments-support/computing-for-science/cs-software/all-software/matlab-ill/
* %Link
* Restrax at http://omega.ujf.cas.cz/restrax/
* %End
*******************************************************************************/

DEFINE INSTRUMENT templateTAS(
  KI=2.278,EN=0,
  L1=9, L2=2.1, L3=1.5, L4=0.7,
  SM=1, SS=-1, SA=1,
  DM=3.3539, DA=3.3539,
  RMV=-1, RMH=0, RAV=0, RAH=-1,
  ETAM=30, ETAA=30,
  ALF1=60, ALF2=60, ALF3=60, ALF4=60,
  BET1=120, BET2=120, BET3=120, BET4=120,
  verbose=1,
  A1=0,A2=0,A3=0,A4=0,A5=0,A6=0,
  NHM=9, NVM=9, WM=0.10, HM=0.12,
  NHA=9, NVA=9, WA=0.10, HA=0.12,
  radius=0.01, height=0.05, WB=0.10, HB=0.10, WD=0.05,HD=.085
)

DECLARE
%{
	  double EI=0, EF=0;
	  double flag_analyzer=0;
	  double flag_env=0;
%}
/* end of DECLARE */

INITIALIZE
%{
	double Vi;

  Vi = K2V*fabs(KI);
  EI = VS2E*Vi*Vi;
  EF = EI - EN;
  
  A1 *= SM;
	A2 *= SM;
	/* A3 = <KI,A> is determined in absolute angle */
	A4 *= SS;
	A5 *= SA;
	A6 *= SA;

	if (verbose) {
	  printf("%s: Detailed TAS configuration\n", NAME_CURRENT_COMP);
	}

	/* compute optimal curvatures */
	double L;
	L = 1/(1/L1+1/L2);
	if (RMV < 0) RMV = fabs(2*L*sin(DEG2RAD*A1));
	if (RMH < 0) RMH = fabs(2*L/sin(DEG2RAD*A1));
	L = 1/(1/L3+1/L4);
	if (RAV < 0) RAV = fabs(2*L*sin(DEG2RAD*A5));
	if (RAH < 0) RAH = fabs(2*L/sin(DEG2RAD*A5));

	if (verbose) {
		printf("Angles: A1=%.4g A2=%.4g A3=%.4g A4=%.4g A5=%.4g A6=%.4g [deg]\n",
		    A1, A2,
		    A3, A4,
		    A5, A6);

	  printf("Monochromator: DM=%.4g [Angs] RMH=%.4g [m] RMV=%.4g [m] %s\n",
	    DM, RMH, RMV,
	    (!RMH && !RMV ? "flat" : "curved"));

	  printf("Analyzer:      DA=%.4g [Angs] RAH=%.4g [m] RAV=%.4g [m] %s\n",
			DA, RAH, RAV,
	    (!RAH && !RAV ? "flat" : "curved"));

	  printf("EI=%g EF=%g meV\n",EI, EF);

	}
	RMH=fabs(RMH)*SM;
	RMV=fabs(RMV)*SM;
	RAH=fabs(RAH)*SA;
	RAV=fabs(RAV)*SA;
	
%}
/* end of INITIALIZE */

TRACE
/* Source description */

COMPONENT Origin=Progress_bar()
AT (0,0,0) ABSOLUTE

/* a flat constant source */
COMPONENT Source = Source_gen(
  xwidth = WB, yheight=HB,
  dist = L1,
  focus_xw = fabs(WM*sin(A1*DEG2RAD))+1e-3, focus_yh = HM,
  E0 = EI,
  dE = EI*0.03)
AT (0,0,0) ABSOLUTE

COMPONENT SC1 = Collimator_linear(
  xmin =-WB/2, ymin =-HB/2,
  xmax = WB/2, ymax = HB/2,
  length = L1/2,
  divergence=ALF1,
  divergenceV=BET1)
WHEN (ALF1 && BET1)
AT (0, 0, L1/4) ABSOLUTE

COMPONENT Guide_out=Arm()
AT (0, 0, L1) ABSOLUTE

COMPONENT Mono_Cradle = Arm()
  AT (0, 0, 0) RELATIVE PREVIOUS
  
SPLIT COMPONENT PG1Xtal = Monochromator_curved(
  width  = WM,
  height = HM,
  NH=NHM, NV=NVM,
  RV=RMV, RH=RMH,
  DM=DM, mosaich = ETAM, mosaicv = ETAM)
AT (0, 0, 0) RELATIVE Mono_Cradle
ROTATED (0, A1, 0) RELATIVE Mono_Cradle

/*                                on mono, pointing towards sample */
COMPONENT Mono_Out = Arm()
  AT (0,0,0) RELATIVE Mono_Cradle
  ROTATED (0, A2, 0) RELATIVE Mono_Cradle
/* 
COMPONENT D4_SC2_1D = Monitor_nD(
  xmin = -0.08/2, xmax = 0.08/2,
  ymin = -0.1200/2, ymax = 0.1200/2,
  options="lambda, cm2, auto", restore_neutron=1)
AT (0, 0, L2/3) RELATIVE Mono_Out
*/
COMPONENT SC2 = Collimator_linear(
  xmin =-fabs(WM*sin(A1*DEG2RAD))/2+1e-3, ymin =-HM/2,
  xmax = fabs(WM*sin(A1*DEG2RAD))/2+1e-3, ymax = HM/2,
  length = 0.35,
  divergence=ALF2,
  divergenceV=BET2)
WHEN (ALF2 && BET2)
AT (0, 0, L2/2) RELATIVE Mono_Out

COMPONENT Sample_Cradle = Monitor_nD(xwidth=0.01, yheight=0.01, options="per cm2", restore_neutron=1)
  AT (0, 0, L2) RELATIVE Mono_Out
  ROTATED (0, A3, 0) RELATIVE Mono_Out

SPLIT COMPONENT Sample = Res_sample(
    radius = radius, yheight = height, 
    target_index=+3, focus_aw=fabs(RAD2DEG*atan2(WA*sin(A5*DEG2RAD),L3)), focus_ah=RAD2DEG*atan2(HA,L3), 
    E0=EF, dE=0)
  AT (0, 0, 0) RELATIVE PREVIOUS

COMPONENT Sample_Out = Arm() /*        this is the sample-ana axis */
  AT (0,0,0) RELATIVE Sample_Cradle
  ROTATED (0, A4, 0) RELATIVE Mono_Out
/*
COMPONENT D7_SC3_1D = Monitor_nD(
  xwidth = 0.08, yheight = HA, bins=50, restore_neutron=1,
  options="theta energy, all auto, banana")
AT (0, 0, 0) RELATIVE Sample_Out
*/
COMPONENT SC3 =Collimator_linear(
  xmin =-fabs(WA*sin(A5*DEG2RAD))/2+1e-3, ymin =-HA/2,
  xmax = fabs(WA*sin(A5*DEG2RAD))/2+1e-3, ymax = HA/2,
  length = 0.40,
  divergence=ALF3,
  divergenceV=BET3)
WHEN (ALF3 && BET3)
AT (0, 0, L3/2) RELATIVE Sample_Out

COMPONENT Ana_Cradle = Arm()
  AT (0, 0, L3) RELATIVE Sample_Out

COMPONENT PG2Xtal = Monochromator_curved(
  width  = WA,
  height = HA,
  NH=NHA, NV=NVA,
  RV=RAV, RH=RAH,
  DM=DA, mosaich = ETAA, mosaicv = ETAA)
AT (0, 0, 0) RELATIVE Ana_Cradle
ROTATED (0, A5, 0) RELATIVE Ana_Cradle

COMPONENT Ana_Out = Arm() /*        this is the sample-ana axis */
  AT (0,0,0) RELATIVE Ana_Cradle
  ROTATED (0, A6, 0) RELATIVE Ana_Cradle

COMPONENT SC4 =Collimator_linear(
  xmin =-fabs(WA*sin(A5*DEG2RAD))/2+1e-3, ymin =-HA/2,
  xmax = fabs(WA*sin(A5*DEG2RAD))/2+1e-3, ymax = HA/2,
  length = 0.24,
  divergence=ALF4,
  divergenceV=BET4)
WHEN (ALF4 && BET4)  
AT (0, 0, (L4-0.24)/2) RELATIVE Ana_Out

/* vertical 3He Detector */
// COMPONENT He3H = PSD_monitor(
//   xmin = -0.025400, xmax = 0.025400,
//   ymin = -0.042850, ymax = 0.042850,
//   nx=20, ny=20, filename="He3H.psd")
// AT (0, 0, machine_hkl.l4) RELATIVE Ana_Out

COMPONENT He3H = Res_monitor(filename="resolution.dat",
  xwidth=WD, yheight=HD,
  res_sample_comp=Sample)
AT (0, 0, L4) RELATIVE Ana_Out

END
